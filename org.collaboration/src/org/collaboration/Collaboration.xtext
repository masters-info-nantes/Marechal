grammar org.collaboration.Collaboration with org.eclipse.xtext.common.Terminals

generate collaboration "http://www.collaboration.org/Collaboration"

File:
	(collabs += Collaboration ';')*
;

Collaboration:
	'collaboration' name = ID ('extends' parent = [Collaboration])?
	'{'
		(items += (Role | Connector | CollaborationUse) ';')*
	'}'
;

Role:
	'role' name = ID (':' type = ID)?
;

Connector:
	'connector' '(' role1 = [Role] ',' role2 = [Role] ')'
;

CollaborationUse:
	'collaborationUse' name = ID ':' collab = [Collaboration]
	'{'
		(roles += RoleAffectation ';')*
	'}'
;

RoleAffectation:
	use = [Role|QualifiedName] '=' host = [Role]
;

QualifiedName:
	ID ('.' ID)*
;